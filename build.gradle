plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.11.2'
}

repositories {
    mavenCentral()
}

def aspectjWeaverVersion = "1.9.22",
    selenideVersion = "6.13.0",
    restAssuredVersion = "5.3.0",
    allureVersion = "2.21.0",
    junitVersion = "5.9.3",
    slf4jVersion = "2.0.7"

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

dependencies {
    testImplementation(
            "org.aspectj:aspectjweaver:$aspectjWeaverVersion",
            "com.codeborne:selenide:$selenideVersion",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "io.appium:java-client:8.3.0",
            "io.rest-assured:rest-assured:$restAssuredVersion",
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "org.slf4j:slf4j-simple:$slf4jVersion",
            'org.aeonbits.owner:owner:1.0.4'
    )
    testCompileOnly('org.projectlombok:lombok:1.18.38')
    testAnnotationProcessor('org.projectlombok:lombok:1.18.38')
}

tasks.withType(Test) {
    useJUnitPlatform()

    systemProperties(System.getProperties())

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

def android = "android",
    wiki = "wiki",
    regress = "regress",
    smoke = "smoke"


static def setupBrowserstack(task) {
    task.systemProperty "os", "android"
    task.systemProperty "env", "browserstack"
}

def browserstackTasks = ['smoke_browserStack_android_wiki_task',
                         'regress_browserStack_android_wiki_task',
                         'all_browserStack_android_wiki_task']

tasks.matching { t -> browserstackTasks.contains(t.name) }.configureEach { task ->
    setupBrowserstack(task)
}

tasks.register('regress_emulator_android_wiki_task', Test) {
    useJUnitPlatform {
        includeTags(android, wiki, regress)
        excludeTags(smoke)
    }
}

tasks.register('smoke_emulator_android_wiki_task', Test) {
    useJUnitPlatform {
        includeTags(android, wiki, smoke)
    }
}

tasks.register('all_emulator_android_wiki_task', Test) {
    useJUnitPlatform {
        includeTags(android, wiki)
    }
}

tasks.register('regress_browserStack_android_wiki_task', Test) {
    useJUnitPlatform {
        includeTags(android, wiki, regress)
        excludeTags(smoke)
    }
}

tasks.register('smoke_browserStack_android_wiki_task', Test) {
    useJUnitPlatform {
        includeTags(android, wiki, smoke)
    }
}

tasks.register('all_browserStack_android_wiki_task', Test) {
    useJUnitPlatform {
        includeTags(android, wiki)
    }
}