plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.11.2'
}

repositories {
    mavenCentral()
}

def aspectjWeaverVersion = "1.9.22",
    selenideVersion = "6.13.0",
    restAssuredVersion = "5.3.0",
    allureVersion = "2.21.0",
    junitVersion = "5.9.3",
    slf4jVersion = "2.0.7"

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

dependencies {
    testImplementation(
            "org.aspectj:aspectjweaver:$aspectjWeaverVersion",
            "com.codeborne:selenide:$selenideVersion",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "io.appium:java-client:8.3.0",
            "io.rest-assured:rest-assured:$restAssuredVersion",
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "org.slf4j:slf4j-simple:$slf4jVersion",
            'org.aeonbits.owner:owner:1.0.4'
    )
}

tasks.withType(Test) {
    useJUnitPlatform()

    systemProperties(System.getProperties())

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

def android = "android",
    wiki = "wiki",
    regress = "regress",
    smoke = "smoke"

tasks.register('android_wiki_regress_task', Test) {
    useJUnitPlatform {
        includeTags(android, wiki, regress)
        excludeTags(smoke)
    }
}

tasks.register('android_wiki_smoke_task', Test) {
    useJUnitPlatform {
        includeTags(android, wiki, smoke)
    }
}

tasks.register('android_wiki_smoke_and_regress_task', Test) {
    useJUnitPlatform {
        includeTags(android, wiki, smoke,regress)
    }
}